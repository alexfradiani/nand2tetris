// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/RAM8.hdl
/**
 * Memory of eight 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.
 */
CHIP RAM8 {
    IN in[16], load, address[3];
    OUT out[16];

    PARTS:
    // Distribute the input to the address being evaluated
    DMux8Way(
        in=load, sel=address, a=reg0load, b=reg1load, 
        c=reg2load, d=reg3load,
        e=reg4load, f=reg5load, g=reg6load, h=reg7load);

    // update or read the respective register
    Register(in=in, load=reg0load, out=reg0out);
    Register(in=in, load=reg1load, out=reg1out);
    Register(in=in, load=reg2load, out=reg2out);
    Register(in=in, load=reg3load, out=reg3out);
    Register(in=in, load=reg4load, out=reg4out);
    Register(in=in, load=reg5load, out=reg5out);
    Register(in=in, load=reg6load, out=reg6out);
    Register(in=in, load=reg7load, out=reg7out);

    // Converge into one output
    Mux8Way16(a=reg0out, b=reg1out, c=reg2out, 
        d=reg3out, e=reg4out, f=reg5out, 
        g=reg6out, h=reg7out, sel=address, out=out);
}
